import lara.util.Logger;

/**
 * Adds calls to a timer between a section of code. By default, measure is in milliseconds.
 */
aspectdef MeasureTime
	input $jp, message, filename, unit end

	// TODO: normalize and check unit with an enum (e.g., TimeUnit - day, second, etc)
	
	if(unit === undefined) {
		unit = "milliseconds";
	} else {
		unit = unit.toLowerCase();
	}
	
	var logger = new Logger(false, filename);
	
	// Build message
	if(message === undefined) {
		// Try to get a name
		var name = $jp.name;
		if(name === undefined) {
			message = "time (us): ";
		} else {
			message = $jp.joinpointType + " \"" + name + "\" time (us): ";
		}
		
	}


	$file = $jp.ancestor("file");
		
	if($file === undefined) {
		println("Could not find the corresponding file of the given joinpoint: " + $jp);
		return;
	}
	
	// Check if C++
	if(!$file.isCxx) {
		println("Cannot measure timming on " + $file.name + ", not yet implemented for C files, only C++");
		return;
	}
	
	// Add include
	$file.addInclude("chrono", true);
	
	select $jp end
	apply
		var timmingId = _nextClavaTimmingId();
		var startVar = "clava_timming_start_" + timmingId;
		var endVar = "clava_timming_end_" + timmingId;
	
		$jp.insert before "chrono::high_resolution_clock::time_point " + startVar +" = chrono::high_resolution_clock::now();";
		
		var durantionVar = "clava_timming_durantion_" + timmingId;
		var durationCalc = "chrono::duration_cast<chrono::" + unit +">( " + endVar + " - " + startVar + " ).count()";
		$timmingResult = AstFactory.exprLiteral(durationCalc);
		$timmingResultDecl = AstFactory.varDecl(durantionVar, $timmingResult);
		
		// Build message
		logger.add(message).addDouble(durantionVar);
		
		// 'after' must be inserted in reverse order		
		$jp.exec insertAfter($timmingResultDecl);
		$jp.insert after "chrono::high_resolution_clock::time_point " + endVar +" = chrono::high_resolution_clock::now();";

		logger.log($timmingResultDecl);
		
	end
	
end


// Global id for timers
var _clava_timming_id = 0;

function _nextClavaTimmingId() {
	var id = _clava_timming_id;
	_clava_timming_id++;
	return id;
}
